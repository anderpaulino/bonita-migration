import groovy.sql.Sql

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'org.bonitasoft.migration'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext{
    sourceversion = System.getProperty("source.version","6.0.2")
    targetversion = System.getProperty("target.version","6.1.0")
    dbvendor = System.getProperty("db.vendor", "mysql")
    dburl = System.getProperty("db.url","jdbc:mysql://localhost:3306/bonita_migration?allowMultiQueries=true");
    dbuser=System.getProperty("db.user","root")
    dbpassword=System.getProperty("db.password", "root")
    dbdriverClass=System.getProperty("db.driverClass","com.mysql.jdbc.Driver")
    bonitahome="${buildDir.path}/home"
    distDir="${buildDir.path}/dist"
    //used for oracle: creates table not the same way
    dbRootUser = System.getProperty("db.root.user")
    dbRootPassword = System.getProperty("db.root.password")
}

configurations {
    sourcefiller
    migrationdist
    drivers
    homezip
}

repositories {
    flatDir name: 'localRepository', dirs: 'lib'
    mavenLocal()
    //used in jenkins: add in system property $ {JENKINS_HOME}/userContent/m2_repo
    if(System.getProperty("maven.repository")!=null){
        def customRepo = System.getProperty("maven.repository")
        println "using custom maven repository: $customRepo"
        maven { url customRepo }
    }else{
        println "no custom repository defined, use system property 'maven.repository' to add one"
    }
    mavenCentral()
}
dependencies {
    sourcefiller "org.bonitasoft.migration:bonita-migration-db-filler-${sourceversion}:1.0.0-SNAPSHOT"
    homezip  group: 'org.bonitasoft.engine', name: 'bonita-home', version: sourceversion, ext: 'zip'
    testCompile "org.bonitasoft.migration:bonita-migration-db-filler-${targetversion}:1.0.0-SNAPSHOT"
    migrationdist group: 'org.bonitasoft.migration', name: "bonita-migration-distrib", version: "1.0.0-SNAPSHOT", ext: 'zip'
    drivers group: 'mysql', name: 'mysql-connector-java', version: '5.1.26'
    drivers group: 'org.postgresql', name: 'postgresql', version: '9.2-1003-jdbc4'
    drivers name:'ojdbc6'
    drivers name:'sqljdbc-4.0.2206.100'
    sourcefiller configurations.drivers
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile configurations.drivers
}
//allow to execute sql from the build script -> see cleardb task
URLClassLoader loader = GroovyObject.class.classLoader
configurations.drivers.each {File file ->
    loader.addURL(file.toURI().toURL())
}
//drop and create the database
task cleardb<<{

    if(dbvendor.equals("oracle")){
        println "cleaning oracle database $dbuser"
        if(dbRootUser == null || dbRootUser.isEmpty() || dbRootPassword == null || dbRootPassword.isEmpty()){
            throw new IllegalStateException("must specify db.root.user and db.root.password for oracle")
        }
        def props = [user: dbRootUser, password: dbRootPassword] as Properties
        def sql = Sql.newInstance(dburl, props, dbdriverClass)
        try{
            sql.executeUpdate("DROP user "+dbuser+" cascade");
        }catch(Exception e){
            println "can't drop database, maybe it did not exist"
        }
        //do not use gstring because it create prepared statement
        sql.executeUpdate("CREATE USER "+dbuser+" IDENTIFIED BY "+dbpassword);
        sql.executeUpdate("GRANT connect, resource TO "+dbuser+" IDENTIFIED BY "+dbpassword);
        sql.executeUpdate("GRANT select ON sys.dba_pending_transactions TO "+dbuser);
        sql.executeUpdate("GRANT select ON sys.pending_trans\$ TO "+dbuser);
        sql.executeUpdate("GRANT select ON sys.dba_2pc_pending TO "+dbuser);
        sql.executeUpdate("GRANT execute ON sys.dbms_system TO "+dbuser);
    }else{
        println "dburl=$dburl"
        def parsedUrl = (dburl =~ /(jdbc:\w+:\/\/[\w\d\.]+:\d+\/)([\w-_\d]+).*/)
        def genericUrl = parsedUrl[0][1]
        def databaseName = parsedUrl[0][2]
        println "recreate database $databaseName on $genericUrl with driver $dbdriverClass"
        def props = [user: dbuser, password: dbpassword] as Properties
        def sql = Sql.newInstance(genericUrl, props, dbdriverClass)
        try{
            sql.executeUpdate("drop database `"+databaseName+"`")
        }catch(Exception e){
            println "can't drop database, maybe it did not exist"
        }
        sql.executeUpdate("create database `"+databaseName+"` DEFAULT CHARACTER SET utf8")
    }
}
task unpackhome(dependsOn:cleardb,type: Copy){
    def zipFile = configurations.homezip.files.iterator().next()
    def outputDir = file("${buildDir.path}")

    from zipTree(zipFile)
    into outputDir
}

task execfill(dependsOn:unpackhome, type: JavaExec) {
    doFirst { println "execute fill of the database using classpath of $sourceversion" }
    classpath configurations.sourcefiller
    systemProperties(["sysprop.bonita.db.vendor":dbvendor,
        "jdbc.vendor":dbvendor,
        "jdbc.url":dburl,
        "jdbc.user":dbuser,
        "jdbc.password":dbpassword,
        "jdbc.driverClass":dbdriverClass,
        "bonita.home":bonitahome])
    main = "org.bonitasoft.migration.DatabaseFiller"
}
task unpackdist(dependsOn:execfill, type: Copy){
    def zipFile = configurations.migrationdist.files.iterator().next()
    def outputDir = file(distDir)

    from zipTree(zipFile)
    into outputDir
}
task copyDrivers(dependsOn:unpackdist, type: Copy){
    into "$distDir/lib"
    from configurations.drivers
}

task changeproperties(dependsOn:copyDrivers)<<{
    def propFile = new File(distDir,"Config.properties")
    def fis = new FileInputStream(propFile)
    def Properties props = new Properties()
    props.load(fis)
    fis.close()
    props.put("source.version",String.valueOf(sourceversion))
    props.put("target.version",String.valueOf(targetversion))
    props.put("db.vendor",String.valueOf(dbvendor));
    props.put("db.url",String.valueOf(dburl));
    props.put("db.user",String.valueOf(dbuser));
    props.put("db.password",String.valueOf(dbpassword));
    props.put("db.driverClass",String.valueOf(dbdriverClass));
    props.put("bonita.home",String.valueOf(bonitahome));
    props.store(new FileWriter(propFile), "config");
}
task runmigration(dependsOn:changeproperties, type:JavaExec){
    workingDir distDir
    main = 'groovy.lang.GroovyShell'
    classpath = files([
        "build/dist/lib/groovy-all-1.8.6.jar"
    ]as String[])
    args 'Migration.groovy'
    systemProperty 'auto.accept', 'true'
}

test.dependsOn(runmigration)
test {
    systemProperty "sysprop.bonita.db.vendor",dbvendor
    systemProperty "jdbc.vendor",dbvendor
    systemProperty "jdbc.url",dburl
    systemProperty "jdbc.user",dbuser
    systemProperty "jdbc.password",dbpassword
    systemProperty "jdbc.driverClass",dbdriverClass
    systemProperty "bonita.home",bonitahome
    systemProperty "target.version",targetversion
    testLogging.showStandardStreams = true
}


